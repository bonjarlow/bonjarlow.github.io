<html>
	<head>
		<title>My Sketch</title>
		
		<video controls="controls" width="800" height="800"
       name="Video Name" src="https://bonjarlow.github.io/pic_assets/red-white-black-highdscale.mp4"></video>
    
    <!-- Load the Processing.js library -->
		<script src="https://cdnjs.cloudflare.com/ajax/libs/processing.js/1.6.0/processing.min.js"></script>
	</head>
	<body>
		<script type="application/processing">
		
Network web;
color cb;
color cn;
float dist;
float camtheta = 0;
float dt = 0.00;

float eyeX, eyeY, eyeZ;
float centerX, centerY, centerZ;
float upX, upY, upZ;


void setup(){
  size(800,800, P3D);
  colorMode(HSB,360,100,100);
  //cb = color(#3FB9F0); color(#B94F74); color(#B73A34); color(#1D2995);
  //cn = color(#055C83); color(#FFFEFA); color(#FFFFFF);
  //cb = color(#D81A1A);
  cb = color(#931515);
  cn = color(#000000);
  dist = 150;
  background(cb);
  frameRate(30);
  ellipseMode(CENTER);
  sphereDetail(5);
  web = new Network(200, dist, 6);
  camconfig();
}

void draw(){
  background(cb);
  web.update();
  web.connect();
  web.dscale = dist + 100*sin((frameCount%750)*TWO_PI/750);
  noFill();
  stroke(#FFFFFF);
  pushMatrix();
    translate(width/2, height/2);
    box(800,800,800);
  popMatrix();
  stroke(cn);
  //axes();
  camupdate();
  //saveFrame("red-white-black-highdscale/######.png");
}

class Node {
  
  float xpos, ypos, zpos;
  int dir;
  float size;
  float vx, vy, vz;
  
  Node(float x, float y, float z, float size){
    this.xpos = x;
    this.ypos = y;
    this.zpos = z;
    this.size = size;
    float val1 = random(-1,1);
    float val2 = random(-1,1);
    float dir1 = val1/abs(val1);
    float dir2 = val2/abs(val2);
    vx = random(1,2)*dir1;
    vy = random(1,2)*dir2;
    vz = random(1,2)*dir2;
    fill(cn);
    pushMatrix();
      translate(xpos, ypos, zpos);
      sphere(size);
    popMatrix();
  }
  
  void move(){
    xpos += vx;
    ypos += vy;
    zpos += vz;
    if(xpos > (width+5)){
      xpos = 0;
    }
    if(xpos < -5){
      xpos = width;
    }
    if(ypos > (height+5)){
      ypos = 0;
    }
    if(ypos < -5){
      ypos = height;
    }
    if(zpos > (405)){
      zpos = -400;
    }
    if(zpos < -405){
      zpos = 400;
    }
    //fill(cn);
    pushMatrix();
      translate(xpos, ypos, zpos);
      //noFill();
      sphere(size);
    popMatrix();
  }
}

class Network{
  Node[] nodelist;
  int num;
  float dscale;
  float size;
  
  Network(int num, float dscale, float size){
    this.num = num;
    this.dscale = dscale;
    this.size = size;
    nodelist = new Node[num];
    
    for(int i = 0; i < num; i++){
      nodelist[i] = new Node((int)(random(1)*width), (int)(random(1)*height),
      random(-400,400), size);
    }
    
  }
  
  void update(){
    for(int i = 0; i < num; i++){
      nodelist[i].move();
    }
  }
  
  void connect(){
    for(int i = 0; i < num; i++){
      for(int j = 0; j < i; j++){
        float dx = nodelist[i].xpos - nodelist[j].xpos;
        float dy = nodelist[i].ypos - nodelist[j].ypos;
        float dz = nodelist[i].zpos - nodelist[j].zpos;
        float dist = sqrt(dx*dx + dy*dy + dz*dz);
        if(dist < dscale){
          stroke(#FFFFFF, 70);
          line(nodelist[i].xpos, nodelist[i].ypos, nodelist[i].zpos, 
          nodelist[j].xpos, nodelist[j].ypos, nodelist[j].zpos);
        }
      }
    }
  }
  
}

void keyPressed() {
    if (keyCode == UP) {
      //web.dscale += 1;
      eyeZ+=80;
      println("eyeZ: ", eyeZ);
      camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
    } 
    if (keyCode == DOWN) {
      //web.dscale -= 1;
      eyeZ-=80;
      println("eyeZ: ", eyeZ);
      camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
    }
    if (keyCode == LEFT) {
      //web.dscale += 1;
      eyeX+=80;
      println("eyeX: ", eyeX);
      camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
    } 
    if (keyCode == RIGHT) {
      //web.dscale -= 1;
      eyeX-=80;
      println("eyeX: ", eyeX);
      camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
    }
}

void camconfig(){
  eyeX = width/2.0;
  eyeY = height/2.0;
  eyeZ = 1500;
  centerX = width/2.0;
  centerY = height/2.0;
  centerZ = 0;
  upX = 0;
  upY = 1;
  upZ = 0;
  camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
}

void camupdate(){
    eyeZ = 1500*cos(camtheta);
    eyeX = width/2 + 1500*sin(camtheta);
    camera(eyeX, eyeY, eyeZ, centerX, centerY, centerZ, upX, upY, upZ);
    camtheta=PI*sin((frameCount%1500)*TWO_PI/1500);
}

void axes() {
  stroke(0);
  strokeWeight(2);
  line(0,0,0,width,0,0);
  line(0,0,0,0,height,0);
  line(0,0,0,0,0,400);
  strokeWeight(1);
  stroke(cn);
}

		

		</script>
		<canvas> </canvas>
	</body>
</html>
